def buildAsLibrary = project.hasProperty('BUILD_AS_LIBRARY');
def buildAsApplication = !buildAsLibrary
if (buildAsApplication) {
    apply plugin: 'com.android.application'
}
else {
    apply plugin: 'com.android.library'
}

Properties props = new Properties()
if (rootProject.file("${rootDir}/local.properties").exists()) {
    props.load(rootProject.file("${rootDir}/local.properties").newDataInputStream())
}

android {
    compileSdkVersion 35
    defaultConfig {
        if (buildAsApplication) {
            applicationId "org.lethargik.supertux2"
        }
        minSdkVersion 21
        targetSdkVersion 35
        versionCode 7000
        versionName "0.7.0"
        ndkVersion "29.0.14206865"
        externalNativeBuild {
            cmake {
                arguments "-DANDROID_PLATFORM=android-" + android.compileSdk.toString(),
                          "-DCMAKE_VERBOSE_MAKEFILE=On",
                          "-DANDROID_STL=c++_shared",
                          "-DANDROID=On",
                          "-DSUPERTUX_VCPKG_ROOT=" + props.getProperty("vcpkg_root", ""),
                          "-DSUPERTUX_NDK_HOME=" + props.getProperty("ndk_home", ""),
                          "-DVCPKG_MANIFEST_MODE=OFF",
                          "-DVCPKG_VERBOSE=ON",
                          "-DENABLE_OPENGL=OFF",
                          "-DANDROID_ALLOW_UNDEFINED_SYMBOLS=OFF", // Literally what the fuck
                          //"--debug-find", // If something isn't being found, uncomment this
                          "--log-level", "TRACE",
                          "-Wno-dev"
                abiFilters rootProject.ext.cpuarch as String[]
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    applicationVariants.all { variant ->
        tasks["merge${variant.name.capitalize()}Assets"]
            .dependsOn("externalNativeBuild${variant.name.capitalize()}")
    }
    if (!project.hasProperty('EXCLUDE_NATIVE_LIBS')) {
        sourceSets.main {
            jniLibs.srcDir 'libs'
        }
        externalNativeBuild {
            cmake {
                path '../../../CMakeLists.txt'
            }
        }

    }
    lintOptions {
        abortOnError false
    }
    namespace 'org.lethargik.supertux2'

    if (buildAsLibrary) {
        libraryVariants.all { variant ->
            variant.outputs.each { output ->
                def outputFile = output.outputFile
                if (outputFile != null && outputFile.name.endsWith(".aar")) {
                    def fileName = "org.lethargik.supertux2.aar";
                    output.outputFile = new File(outputFile.parent, fileName);
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
}
