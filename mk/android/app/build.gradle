plugins {
    id 'com.android.application'
}

Properties props = new Properties()
if (rootProject.file("${rootDir}/local.properties").exists()) {
    props.load(rootProject.file("${rootDir}/local.properties").newDataInputStream())
}

var APP_ID = "org.supertux.supertux2"

android {
    namespace APP_ID
    compileSdkVersion 35

    defaultConfig {
        applicationId APP_ID
        minSdkVersion 21
        targetSdkVersion 35

        // NOTE: Don't forget to bump the version code!
        versionCode 2
        versionName "0.7.0-beta2"

        ndkVersion "29.0.14206865"
        externalNativeBuild {
            cmake {
                arguments "-DANDROID_PLATFORM=android-" + android.compileSdk.toString(),
                          "-DCMAKE_VERBOSE_MAKEFILE=On",
                          "-DANDROID_STL=c++_shared",
                          "-DANDROID=On",
                          "-DSUPERTUX_VCPKG_ROOT=" + props.getProperty("vcpkg_root", ""),
                          "-DSUPERTUX_NDK_HOME=" + props.getProperty("ndk_home", ""),
                          "-DVCPKG_MANIFEST_MODE=OFF",
                          "-DVCPKG_VERBOSE=ON",
                          "-DENABLE_OPENGL=OFF",
                          "-DANDROID_ALLOW_UNDEFINED_SYMBOLS=OFF", // Literally what the fuck
                          //"--debug-find", // If something isn't being found, uncomment this
                          "--log-level", "TRACE",
                          "-Wno-dev"
                abiFilters rootProject.ext.cpuarch as String[]
            }
            android.defaultConfig.externalNativeBuild.cmake.arguments.addAll(rootProject.ext.cmake_args)
        }
    }

    assetPacks = [":assetpack"]

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    if (!project.hasProperty('EXCLUDE_NATIVE_LIBS')) {
        sourceSets.main {
            jniLibs.srcDir 'libs'
        }
        externalNativeBuild {
            cmake {
                path '../../../CMakeLists.txt'
            }
        }
    }

    lint {
        abortOnError false
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
}
