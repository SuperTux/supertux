if(NOT EXISTS ${CMAKE_CURRENT_SOURCE_DIR}/external/simplesquirrel/CMakeLists.txt)
  message(FATAL_ERROR "simplesquirrel submodule is not checked out or ${CMAKE_CURRENT_SOURCE_DIR}/external/simplesquirrel/CMakeLists.txt is missing")
endif()

set(SIMPLESQUIRREL_PREFIX ${CMAKE_BINARY_DIR}/simplesquirrel/ex)
ExternalProject_Add(simplesquirrel_project
  SOURCE_DIR "${CMAKE_SOURCE_DIR}/external/simplesquirrel/"
  BUILD_BYPRODUCTS
  "${SIMPLESQUIRREL_PREFIX}/bin/${CMAKE_SHARED_LIBRARY_PREFIX}simplesquirrel${CMAKE_SHARED_LIBRARY_SUFFIX}"
  "${SIMPLESQUIRREL_PREFIX}/lib${LIB_SUFFIX}/${CMAKE_STATIC_LIBRARY_PREFIX}simplesquirrel${CMAKE_STATIC_LIBRARY_SUFFIX}"
  CMAKE_ARGS
  -DCMAKE_BUILD_TYPE=${BUILD_TYPE}
  -DCMAKE_C_COMPILER=${CMAKE_C_COMPILER}
  -DCMAKE_C_FLAGS=${CMAKE_C_FLAGS}
  -DCMAKE_CXX_COMPILER=${CMAKE_CXX_COMPILER}
  -DCMAKE_CXX_FLAGS=${CMAKE_CXX_FLAGS}
  -DCMAKE_INSTALL_PREFIX=${SQUIRREL_PREFIX}
  -DCMAKE_INSTALL_LIBDIR=lib
  -DSQUIRREL_INCLUDE_DIR=${SQUIRREL_INCLUDE_DIR}
  -DSQUIRREL_LIBRARIES=${SQUIRREL_LIBRARIES}
  -DSQSTDLIB_LIBRARIES=${SQSTDLIB_LIBRARIES}
  -DINSTALL_INC_DIR=include
  -DBUILD_TESTS=OFF)

# Pre-create directory so that cmake doesn't complain about its non-existance
file(MAKE_DIRECTORY "${SIMPLESQUIRREL_PREFIX}/include/")

if(WIN32)
  add_library(LibSimpleSquirrel SHARED IMPORTED)
  set_target_properties(LibSimpleSquirrel PROPERTIES
    IMPORTED_LOCATION "${SIMPLESQUIRREL_PREFIX}/bin/${CMAKE_SHARED_LIBRARY_PREFIX}simplesquirrel${CMAKE_SHARED_LIBRARY_SUFFIX}"
    INTERFACE_INCLUDE_DIRECTORIES "${SIMPLESQUIRREL_PREFIX}/include/")
else()
  add_library(LibSimpleSquirrel STATIC IMPORTED)
  set_target_properties(LibSimpleSquirrel PROPERTIES
    IMPORTED_LOCATION "${SIMPLESQUIRREL_PREFIX}/lib${LIB_SUFFIX}/${CMAKE_STATIC_LIBRARY_PREFIX}simplesquirrel${CMAKE_STATIC_LIBRARY_SUFFIX}"
    INTERFACE_INCLUDE_DIRECTORIES "${SIMPLESQUIRREL_PREFIX}/include/")
endif()

add_dependencies(LibSimpleSquirrel simplesquirrel_project)

if(WIN32)
  get_property(SIMPLESQUIRREL_LIB_PATH TARGET LibSimpleSquirrel PROPERTY IMPORTED_LOCATION)
  install(FILES ${SIMPLESQUIRREL_LIB_PATH} DESTINATION "${INSTALL_SUBDIR_BIN}")
endif()
